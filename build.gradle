plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.10' apply false
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group = 'com.medly.norm'

    repositories {
        jcenter()
    }


    dependencies {
        implementation platform('org.jetbrains.kotlin:kotlin-bom')
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

        testImplementation 'org.testcontainers:postgresql:1.15.0-rc2'
        testImplementation 'org.postgresql:postgresql:42.2.18'

        testImplementation 'io.kotest:kotest-runner-junit5-jvm:4.3.1'
        testImplementation 'io.kotest:kotest-assertions-core-jvm:4.3.1'
        testImplementation 'io.kotest:kotest-property-jvm:4.3.1'
        testImplementation 'io.kotest:kotest-extensions-testcontainers-jvm:4.3.1'
        testImplementation 'io.kotest:kotest-plugins-pitest:4.3.1'
        testImplementation 'io.kotest:kotest-runner-console-jvm:4.1.3.2'
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "PASSED", "FAILED", "SKIPPED"
            exceptionFormat "full"
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

project(":codegen") {
    dependencies {
        implementation project(":runtime")
        api project(":api")

        api 'org.postgresql:postgresql:42.2.14'
        implementation 'com.squareup:kotlinpoet:1.6.0'

        implementation 'org.atteo:evo-inflector:1.2.2'
        implementation 'org.apache.commons:commons-text:1.9'
    }
}

project(":cli") {
    apply plugin: 'application'

    applicationName = 'norm-codegen'
    mainClassName = "norm.cli.NormCliKt"

    dependencies {
        implementation project(":codegen")
        implementation "com.github.ajalt:clikt:2.7.1"
    }
}
