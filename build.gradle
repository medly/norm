plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group = 'com.medly.norm'

    repositories {
        jcenter()
    }

    dependencies {
        implementation platform('org.jetbrains.kotlin:kotlin-bom')
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
        testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.2'
        testImplementation 'org.testcontainers:postgresql:1.14.3'
        testImplementation 'postgresql:postgresql:9.1-901-1.jdbc4'
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "PASSED", "FAILED", "SKIPPED"
            exceptionFormat "full"
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

project(":codegen") {
    dependencies {
        implementation project(":runtime")

        implementation 'com.squareup:kotlinpoet:1.6.0'
        implementation 'org.atteo:evo-inflector:1.2.2'
        implementation 'org.apache.commons:commons-text:1.8'
        implementation 'org.postgresql:postgresql:42.2.14'

        testImplementation("io.kotlintest:kotlintest-runner-junit5:3.3.0")
    }
}

project(":cli") {
    apply plugin: 'application'

    applicationName = 'norm-codegen'
    mainClassName = "norm.cli.NormCliKt"

    dependencies {
        implementation project(":codegen")
        implementation 'org.postgresql:postgresql:42.2.14'
        implementation "com.github.ajalt:clikt:2.7.1"
    }
}
